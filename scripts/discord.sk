options:
	prefix: ?
	bot: NerdPR
	token: ODI4Mjc0ODgyMTI2Njc2MDA5.YGnNGA.QqQl9vzX_dHqxaFfKjnyRxwEi3c

	chat-channel: 828275594985734184
	log-channel: 827365241687113778
	suggestion-channel: 827365231545024513
	anticheat-channel: 835180339492028417
	punishment-channel: 827365242589151292
	staffchat-channel: 827365238856089600

on guild message received seen by "{@bot}":
	if discord id of event-channel is "{@chat-channel}":
		set {_message} to "%event-message%"
		replace all ">" and "<" in {_message} with ""
		replace all "@" in {_message} with ""
		replace all "&" and "*" and "~" and "`" and "_" in {_message} with ""
		if {_message} is not "":
			broadcast "&7[DISCORD] @%event-member%&7: %{_message}%"
	if discord id of event-channel is "{@log-channel}":
		make console execute command "%event-message%"
	if discord id of event-channel is "{@staffchat-channel}":
		set {_message} to "%event-message%"
		replace all ">" and "<" in {_message} with ""
		replace all "@" in {_message} with ""
		replace all "&" and "*" and "~" and "`" and "_" in {_message} with ""
		if {_message} is not "":
			send "<##4b8bf2>&lSTAFFCHAT <##81aef7>%event-member% &f%{_message}%" to all players where [input has permission "staff.chat"]
	
on chat:
	event is not canceled
	wait 1 tick
	replace all ">" and "<" with "" in message
	replace all "@" with "" in message
	replace all "&" and "*" and "~" and "`" and "_" with "" in message
	send "**%player%**: %message%" to channel with id "{@chat-channel}" with "{@bot}"

on load:
	create vixio bot:
		enable the guild message reactions intent
		enable the direct message reactions intent
		login to "{@token}" with the name "{@bot}"
		mark "{@bot}" as playing "nerdpr.minehut.gg"

on join:
	wait 1 tick
	player is online
	send "**%player% joined the game.**" to channel with id "{@chat-channel}" with "{@bot}"
	set topic of channel with id "{@chat-channel}" to "**Online:** %size of all players%%nl%**TPS:** %tps from last 1 minute% %tps from last 5 minutes% %tps from last 15 minutes%%nl%**Unique:** %size of all offline players%" with "{@bot}"

on quit:
	wait 1 tick
	quit message is not ""
	send "**%player% left the game.**" to channel with id "{@chat-channel}" with "{@bot}"
	set topic of channel with id "{@chat-channel}" to "**Online:** %size of all players%%nl%**TPS:** %tps from last 1 minute% %tps from last 5 minutes% %tps from last 15 minutes%%nl%**Unique:** %size of all offline players%" with "{@bot}"

on command:
	command is not "hd" or "broadcast"
	send "**%player ? ""Console""%** issued command `%command% %arguments ? """"%`" to channel with id "{@log-channel}" with "{@bot}"

command /suggest <text>:
	cooldown: 15 seconds
	cooldown message: &cThis command is on cooldown.
	usage: &cUsage: /suggest <suggestion>
	trigger:
		send "<##4b8bf2>&lSUGGESTION &8| &7Your suggestion has been submitted."
		create embed:
			set the colour of the embed to purple
			add field named "Player" with value "%player%" to embed
			add field named "Suggestion" with value arg-1 to embed
		send last created embed to channel with id "{@suggestion-channel}" with "{@bot}" and store it in {_msg}
		add reactions ":thumbsup:" and ":thumbsdown:" to {_msg} with "{@bot}"

command /link:
	trigger:
		if {linked::%uuid of player%} is not set:
			set {_e} to random 8 char string from charset `a-zA-Z0-9`
			set {code::%{_e}%} to player
			send formatted "<##4b8bf2>&lDISCORD &8| &7You can link your account in ##bot-commands on our discord using <##81aef7><suggest command:?link %{_e}%>?link %{_e}%<reset>&7."
		else:
			send "<##4b8bf2>&lDISCORD &8| &7Your account is already linked."

discord command ?link [<text>]:
	trigger:
		if arg 1 is not set:
			reply with "Please specify a code."
			stop
		else if {code::%arg-1%} is not set:
			reply with "This code does not exist."
			stop
		else if {code::%arg-1%} is set:
			if {discord::%discord id of event-member%::player} is not set:
				set {discord::%discord id of event-member%::player} to {code::%arg-1%}
				set {discord::%discord id of event-member%} to discord id of event-member
				reply with "Your account was linked to `%{code::%arg-1%}%`."
				send "<##4b8bf2>&lDISCORD &8| &7Your account was linked to %event-member%&7." to {code::%arg-1%}
				set {_uuid} to uuid of {code::%arg-1%}
				set {linked::%{_uuid}%} to true
				delete {code::%arg-1%}
				add role with id "836354088081031230" to roles of event-member
			else:
				reply with "Your account is already linked."
			stop

command /discord:
	trigger:
		send "<##4b8bf2>&lDISCORD &8| &7Click <link:https://discord.gg/8BQd8m2Z6B><##81aef7>here<reset>&7 to join the discord."
