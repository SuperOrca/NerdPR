options:
	prefix: ?
	bot: NerdPR
	token: ODI4Mjc0ODgyMTI2Njc2MDA5.YGnNGA.QqQl9vzX_dHqxaFfKjnyRxwEi3c

	chat-channel: 828275594985734184
	log-channel: 827365241687113778
	suggestion-channel: 827365231545024513
	anticheat-channel: 835180339492028417
	punishment-channel: 827365242589151292

function discordLog(t: text, m: text):
	if {_t} is "chat":
		send {_m} to channel with id "{@chat-channel}" with "{@bot}"
	else if {_t} is "log":
		send {_m} to channel with id "{@log-channel}" with "{@bot}"
	else if {_t} is "anticheat":
		send {_m} to channel with id "{@anticheat-channel}" with "{@bot}"
	else if {_t} is "punish":
		send {_m} to channel with id "{@punishment-channel}" with "{@bot}"

on guild message received seen by "{@bot}":
	if discord id of event-channel is "{@chat-channel}":
		set {_message} to "%event-message%"
		replace all ">" and "<" in {_message} with ""
		replace all "@" in {_message} with ""
		replace all "&" and "*" and "~" and "`" and "_" in {_message} with ""
		if {_message} is not "":
			broadcast "&7%event-member%&7: %{_message}%"
	if discord id of event-channel is "{@log-channel}":
		make console execute command "%event-message%"

on chat:
	event is not canceled
	wait 1 tick
	replace all ">" and "<" with "" in message
	replace all "@" with "" in message
	replace all "&" and "*" and "~" and "`" and "_" with "" in message
	send "**%player%**: %message%" to channel with id "{@chat-channel}" with "{@bot}"

on load:
	create vixio bot:
		enable the guild message reactions intent
		enable the direct message reactions intent
		login to "{@token}" with the name "{@bot}"
		mark "{@bot}" as playing "nerdpr.minehut.gg"

on join:
	wait 1 tick
	player is online
	send "**%player% joined the game.**" to channel with id "{@chat-channel}" with "{@bot}"
	set topic of channel with id "{@chat-channel}" to "**Online:** %size of all players%\n**TPS:** %tps from last 1 minute% %tps from last 5 minutes% %tps from last 15 minutes%\n**Unique:** %size of all offline players%" with "{@bot}"

on quit:
	wait 1 tick
	quit message is not ""
	send "**%player% left the game.**" to channel with id "{@chat-channel}" with "{@bot}"
	set topic of channel with id "{@chat-channel}" to "**Online:** %size of all players%\n**TPS:** %tps from last 1 minute% %tps from last 5 minutes% %tps from last 15 minutes%\n**Unique:** %size of all offline players%" with "{@bot}"

on command:
	command is not "hd" or "broadcast"
	send "**%player ? ""Console""%** issued command `%command% %arguments ? """"%`" to channel with id "{@log-channel}" with "{@bot}"

command /suggest <text>:
	cooldown: 15 seconds
	cooldown message: &cThis command is on cooldown.
	usage: &cUsage: /suggest <suggestion>
	trigger:
		send "<##4b8bf2>&lSUGGESTION&7 Your suggestion has been submitted."
		create embed:
			set the colour of the embed to purple
			add field named "Player" with value "%player%" to embed
			add field named "Suggestion" with value arg-1 to embed
		send last created embed to channel with id "{@suggestion-channel}" with "{@bot}" and store it in {_msg}
		add reactions ":thumbsup:" and ":thumbsdown:" to {_msg} with "{@bot}"

command /discord:
	trigger:
		send "<##4b8bf2>&lDISCORD&7 Click <link:https://discord.gg/8BQd8m2Z6B><##81aef7>here<reset>&7 to join the discord."
