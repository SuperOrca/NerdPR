on load:
	delete {words::*}
	set {words::1} to "prisons"
	set {words::2} to "mine"
	set {words::3} to "orca"
	set {words::4} to "flames"
	set {words::5} to "pickaxe"
	set {words::6} to "nerd"
	set {words::7} to "brain"
	set {words::8} to "lightbulb"
	set {words::9} to "keyboard"
	set {words::10} to "mouse"
	set {words::11} to "webcam"

	delete {mines::*}
	set {mines::1} to {spawn}
	set {mines::2} to location(-366, 85, -517, world "world")
	set {mines::3} to location(-923, 69, -450, world "world")

command /mine:
	trigger:
		open virtual chest with 3 rows named "<##4b8bf2>&lMINES" to player
		fillSlots(player, 3)
		format gui slot 10 of player with white terracotta named "&a&lMINE 1" to run:
			teleport player to {mines::1}
		format gui slot 11 of player with yellow terracotta named "&c&lMINE 2"
		if {prestige::%player's uuid%} >= 2:
			format gui slot 11 of player with yellow terracotta named "&a&lMINE 2" to run:
				teleport player to {mines::2}

on tab complete:
	if event-string is "/skript" or "/sk":
		set tab completions for position 1 to "reload", "enable", "disable", "update" and "help"
		set tab completions for position 2 to all scripts
	if event-string is "/voucher":
		set tab completions for position 1 to "money" and "rank"

function updateScoreboard(p: player):
	set {_u} to {_p}'s uuid
	set title of {_p}'s scoreboard to "<##4b8bf2>&lNERDPR &7&o(%size of all players%)"
	set line 15 of {_p}'s scoreboard to ""
	set {_level} to insertComma({level::%{_u}%} ? 0)
	set line 14 of {_p}'s scoreboard to "&8| &7Level: <##81aef7>%{_level}%"
	set {_prestige} to insertComma({prestige::%{_u}%} ? 0)
	set line 13 of {_p}'s scoreboard to "&8| &7Prestige: <##81aef7>%{_prestige}%"
	set {_rebirth} to insertComma({rebirth::%{_u}%} ? 0)
	set line 12 of {_p}'s scoreboard to "&8| &7Rebirth: <##81aef7>%{_rebirth}%"
	set line 11 of {_p}'s scoreboard to ""
	set {_mined} to formatNumber({mined::%{_u}%} ? 0)
	set line 10 of {_p}'s scoreboard to "&8| &7Mined: <##81aef7>%{_mined}%"
	set {_balance} to formatNumber({balance::%{_u}%} ? 0)
	set line 9 of {_p}'s scoreboard to "&8| &7Balance: <##81aef7>$%{_balance}%"
	set line 8 of {_p}'s scoreboard to "&8| &7Multiplier: <##81aef7>1x"
	set line 7 of {_p}'s scoreboard to ""
	set line 6 of {_p}'s scoreboard to "&7&onerdpr.minehut.gg"

on join:
	set join message to "<##81aef7>%player%&7 joined the game."
	wait 1 tick
	player is online
	if player's inventory does not contain diamond pickaxe:
		give player updatePickaxe(diamond pickaxe named "<##4b8bf2>&lPICKAXE" with nbt "{HideFlags:7b}")
	toggle scoreboard of player to on
	loop all players:
		updateScoreboard(loop-player)

on quit:
	if {ban::%player's uuid%::type} is not set:
		set quit message to "<##81aef7>%player%&7 left the game."
		loop all players:
			updateScoreboard(loop-player)
	else:
		set quit message to ""

command /getpick:
	permission: staff.getpick
	permission message: &cNo Permission.
	trigger:
		give player updatePickaxe(diamond pickaxe named "<##4b8bf2>&lPICKAXE" with nbt "{HideFlags:7b}")

command /getmaxedpick:
	permission: op
	permission message: &cNo Permission.
	trigger:
		give player updatePickaxe(diamond pickaxe named "&c&lADMIN PICKAXE" with nbt "{HideFlags:7b,efficiency:500,fortune:1000000,jump:1,flight:1,tokenfinder:500,keyfinder:5000,explosive:500,jackhammer:1000,chunk:5000,tsunami:10000}")

on item damage:
	cancel event

on hunger meter change:
	cancel event

on offhand switch:
	cancel event

on drop:
	cancel event

command /playtime <player=%player%>:
	trigger:
		send "<##81aef7>%arg-1%'s&7 playtime is <##81aef7>%time played of arg-1%&7."

command /reloadall:
	permission: *
	permission message: &cNo Permission.
	trigger:
		set {_s} to now
		loop all scripts where [input is not "config.sk" or "color.sk"]:
			set {_m} to now
			reload script loop-value
			send "Took %time since {_m}% to reload %loop-value%."
		send "Took %time since {_s}% to reload all scripts." to all players where [input has permission "*"]

on pressure plate:
	push player forwards at speed 2.5
	push player upwards at speed 1.5

command /rules:
	trigger:
		send centered "don't be a bitch" to player

command /sc <text>:
	permission: staff.chat
	permission message: &cNo Permission.
	usage: &cUsage: /sc <text>
	trigger:
		send "&7&lSTAFFCHAT <##81aef7>%player% &7%arg-1%" to all players where [input has permission "staff.chat"]

command /ad:
	trigger:
		send "<##4b8bf2>&lAD&7 Click <suggest command:/ad NerdPR Custom Prison Server><##81aef7>here<reset>&7 to get the advertisment command you can use in the hub."

every 5 minutes:
	delete {reaction::*}
	set {reaction::word} to random element out of {words::*}
	set {reaction::start} to now
	broadcast "<##4b8bf2>&lREACTION&7 Type <##81aef7>%{reaction::word}%&7 to win!"

on chat:
	event is not canceled
	if {reaction::word} is set:
		if message is {reaction::word}:
			cancel event
			broadcast "<##4b8bf2>&lREACTION&7 <##81aef7>%player%&7 typed <##81aef7>%{reaction::word}%&7 in <##81aef7>%time since {reaction::start}%&7."
			delete {reaction::*}

function updateLeaderboards():
	broadcast "<##4b8bf2>&lLEADERBOARDS&7 Leaderboards are not updating. This may cause lag!"
	set {_sorted::*} to sort((indices of {mined::*}), {mined::*})
	set {_total} to formatNumber(sum({mined::*}))
	set {_n} to 0
	loop {_sorted::*}:
		add 1 to {_n}
		set {_p} to loop-value parsed as a offline player
		set {_v} to formatNumber({mined::%loop-value%})
		make console execute command "hd setline mined %{_n} + 1% &7[%{_n}%] <##81aef7>%{_p}% &7(%{_v}%)"
		if {_n} >= 10:
			exit loop
	make console execute command "hd setline mined 12 &7&o(( Total: %{_total}% ))"

every 10 minutes:
	updateLeaderboards()

command /forceleaderboards:
	permission: op
	permission message: &cNo Permission.
	trigger:
		updateLeaderboards()