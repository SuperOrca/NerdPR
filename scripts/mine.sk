options:
	bot: NerdPR

	anticheat-channel: 835180339492028417

function getPrice(b: block) :: number:
	return 500 if {_b} is white terracotta
	return 1000 if {_b} is yellow terracotta
	return 1500 if {_b} is blue terracotta
	return 2000 if {_b} is green terracotta
	return 0

function blockValidator(b: block) :: boolean:
	if {_b} is not air:
		if "%region at {_b}%" contains "mine":
			return true
	return false

function simMine(p: player, t: number):
	set {_u} to {_p}'s uuid
	add {_t} to {balance::%{_u}%}
	add 1 to {xp::%{_u}%}
	add 1 to {mined::%{_u}%}
	set {_xp} to getTag(({_p}'s tool), "xp") + 1
	add "{xp:%{_xp}%}" to nbt of {_p}'s tool
	set {_xpn} to tag "xpn" of nbt of {_p}'s tool
	set {_level} to tag "level" of nbt of {_p}'s tool
	set {_points} to tag "points" of nbt of {_p}'s tool
	if {_xp} >= {_xpn}:
		add "{level:%{_level} + 1%,xpn:%{_xpn}^1.004%,xp:%{_xpn} - {_xp}%,points:%{_points} + 1%}" to nbt of {_p}'s tool
		send "&7Your picked leveled up to level <##81aef7>%{_level} + 1%&7!" to {_p}


function mineActionBar(p: player):
	set {_u} to {_p}'s uuid
	set {_mined} to formatNumber({mined::%{_u}%} ? 0)
	set {_xp} to round ((({xp::%{_u}%} ? 0)/({xpn::%{_u}%} ? 100)) * 100)
	send action bar "&7Progress: <##81aef7>%{_xp}%%% &8| &7Mined: <##81aef7>%{_mined}%" to {_p}

function updateLevel(p: player):
	set {_u} to {_p}'s uuid
	while {xp::%{_u}%} >= ({xpn::%{_u}%} ? 100):
		add 1 to {level::%{_u}%}
		remove ({xpn::%{_u}%} ? 100) from {xp::%{_u}%}
		set {xpn::%{_u}%} to round ({xpn::%{_u}%} ? 100)^1.005
		send "<##f2b84b>&lLEVEL &7Leveled up to level <##f7b481>%{level::%{_u}%}%&7." to {_p}
		wait 1 tick
	while {level::%{_u}%} >= (({prestige::%{_u}%} ? 0) * 5 + 10):
		remove (({prestige::%{_u}%} ? 0) * 5 + 10) from {level::%{_u}%}
		delete {xpn::%{_u}%}
		add 1 to {prestige::%{_u}%}
		broadcast "<##f2b84b>&lPRESTIGE <##f7b481>%{_p}%&7 prestiged to <##f7b481>%{prestige::%{_u}%}%&7."
		wait 1 tick

on break:
	player's tool is pickaxe
	clear drops

	set metadata "nuker" of player to metadata "nuker" of player + 1
	if metadata "nuker_yes" of player is not set:
		set metadata "nuker_yes" of player to true
		wait 5 seconds
		if metadata "nuker" of player > 150:
			send "<##f24b4b>&lANTICHEAT &8| <##f78181>%player%&7 might be using <##f78181>Nuker&7. (Ping: %player's ping%&7) (BPS: %((metadata ""nuker"" of player))/5%&7)" to all players where [input has permission "staff"]
			send "`%player%` might be using **Nuker**. (Ping:%player's ping%) (BPS: %((metadata ""nuker"" of player))/5%)" to channel with id "{@anticheat-channel}" with {@bot}
		if metadata "nuker" of player > 350:
			make console execute command "ban %player% Nuker"
		delete metadata "nuker" of player
		delete metadata "nuker_yes" of player

	set {_price} to getPrice(event-block)
	set {_fort} to random integer between 1 and (getTag(player's tool, "fortune") + 1)
	set {_total} to {_price} * {_fort}
	set {_total} to {_total} * getMulti(player)
	add {_total} to {balance::%player's uuid%}
	simMine(player, {_total})

	set {_loc} to event-block's location

	if (random integer between 1 and (enchantChance("jackhammer"))) <= getTag(player's tool, "jackhammer"):
		set {_loc1} to {_loc} ~ vector(-2, 0, -2)
		set {_loc2} to {_loc} ~ vector(2, 0, 2)
		loop all blocks within {_loc1} and {_loc2}:
			if blockValidator(loop-block) is true:
				simMine(player, {_total})
				set loop-block to air

	if (random integer between 1 and (enchantChance("slice"))) <= getTag(player's tool, "slice"):
		set {_loc1} to {_loc} ~ vector(0, -2, -2)
		set {_loc2} to {_loc} ~ vector(0, 2, 2)
		loop all blocks within {_loc1} and {_loc2}:
			if blockValidator(loop-block) is true:
				simMine(player, {_total})
				set loop-block to air

	if (random integer between 1 and (enchantChance("explosive"))) <= getTag(player's tool, "explosive"):
		loop all blocks in radius 3 around event-block:
			if blockValidator(loop-block) is true:
				simMine(player, {_total})
				set loop-block to air

	if (random integer between 1 and (enchantChance("layer"))) <= getTag(player's tool, "layer"):
		set {_loc1} to {_loc} ~ vector(-20, 0, -20)
		set {_loc2} to {_loc} ~ vector(20, 0, 20)
		loop all blocks within {_loc1} and {_loc2}:
			if blockValidator(loop-block) is true:
				simMine(player, {_total})
				set loop-block to air

	if (random integer between 1 and (enchantChance("laser"))) <= getTag(player's tool, "laser"):
		set {_loc1} to {_loc} ~ vector(20, 0, 0)
		set {_loc2} to {_loc} ~ vector(-20, 0, 0)
		set {_loc3} to {_loc} ~ vector(0, 0, 20)
		set {_loc4} to {_loc} ~ vector(0, 0, -20)
		loop all blocks within {_loc1} and {_loc2}:
			if blockValidator(loop-block) is true:
				simMine(player, {_total})
				set loop-block to air
		loop all blocks within {_loc3} and {_loc4}:
			if blockValidator(loop-block) is true:
				simMine(player, {_total})
				set loop-block to air

	if (random integer between 1 and (enchantChance("cube"))) <= getTag(player's tool, "cube"):
		set {_loc1} to {_loc} ~ vector(1, 1, 1)
		set {_loc2} to {_loc} ~ vector(-1, -1, -1)
		loop all blocks within {_loc} and {_loc2}:
			if blockValidator(loop-block) is true:
				simMine(player, {_total})
				set loop-block to air

	if (random integer between 1 and (enchantChance("xpfinder"))) <= getTag(player's tool, "xpfinder"):
		add {xpn::%player's uuid%} * 1.5 to {xp::%player's uuid%}
		send "<##4b8bf2>&lENCHANT&7 Your <##81aef7>xpfinder&7 has found <##81aef7>%insertComma(round ({xpn::%player's uuid%} * 1.5))%<##81aef7>XP&7."

	if (random integer between 1 and (enchantChance("chunk"))) <= getTag(player's tool, "chunk"):
		loop all blocks within chunk at event-block:
			if blockValidator(loop-block) is true:
				simMine(player, {_total})
				set loop-block to air

	if (random integer between 1 and (enchantChance("tsunami"))) <= getTag(player's tool, "tsunami"):
		loop all blocks in radius 40 around event-block:
			if blockValidator(loop-block) is true:
				simMine(player, {_total})
				set loop-block to air

	set player's tool to updatePickaxeLevel(player's tool)
	mineActionBar(player)
	updateLevel(player)
	updateScoreboard(player)
