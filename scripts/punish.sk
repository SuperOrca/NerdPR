options:
	prefix: <##b24bf2>&lPUNISH &8|

	bot: NerdPR
	punishment-channel: 827365242589151292

on chat:
	if event is not canceled:
		if {mute::%player's uuid%::type} is set:
			if {mute::%player's uuid%::type} is "temp":
				set {_waited} to time since {mute::%player's uuid%::time}
				if {_waited} < {mute::%player's uuid%::span}:
					send "{@prefix} <##b24bf2>&lTEMPMTUE &8|%nl%&7Muted by: <##f781ed>%{mute::%player's uuid%::by}%%nl%&7Reason: <##f781ed>%{mute::%player's uuid%::reason}%%nl%&7Time left: <##f781ed>%difference between {mute::%player's uuid%::span} and {_waited}%"
					cancel event
				else:
					delete {mute::%player's uuid%::*}
			else if {mute::%player's uuid%::type} is "perm":
				send "{@prefix} <##b24bf2>&lMUTE &8|%nl%&7Muted by: <##f781ed>%{mute::%player's uuid%::by}%%nl%&7Reason: <##f781ed>%{mute::%player's uuid%::reason}%"
				cancel event
		if {chatmute} is true:
			if player doesn't have permission "staff":
				send "&cChat is currently muted."
				cancel event

on join:
	if {ban::%player's uuid%::type} is set:
		if {ban::%player's uuid%::type} is "temp":
			set {_waited} to time since {ban::%player's uuid%::time}
			if {_waited} < {ban::%player's uuid%::span}:
				kick player due to "{@prefix} <##b24bf2>&lTEMPBAN &8|%nl%&7Banned by: <##f781ed>%{ban::%player's uuid%::by}%%nl%&7Reason: <##f781ed>%{ban::%player's uuid%::reason}%%nl%&7Time left: <##f781ed>%difference between {ban::%player's uuid%::span} and {_waited}%"
				set join message to ""
			else:
				delete {ban::%player's uuid%::*}
		else if {ban::%player's uuid%::type} is "perm":
			kick player due to "{@prefix} <##b24bf2>&lBAN &8|%nl%&7Banned by: <##f781ed>%{ban::%player's uuid%::by}%%nl%&7Reason: <##f781ed>%{ban::%player's uuid%::reason}%"
			set join message to ""

command /tempban <offline player> <text> <text>:
	permission: punish.tempban
	permission message: &cNo Permission.
	usage: &7Usage: /tempban <player> <time ex:24h> <reason> [-s]
	trigger:
		stop if arg-1 is op
		set {_time} to formatTime(arg-2)
		set {ban::%arg-1's uuid%::type} to "temp"
		set {ban::%arg-1's uuid%::by} to player ? "Console"
		set {ban::%arg-1's uuid%::time} to now
		set {ban::%arg-1's uuid%::span} to {_time}
		set {_reason} to arg-3
		replace all " -s" with "" in {_reason}
		set {ban::%arg-1's uuid%::reason} to {_reason}
		kick arg-1 due to "{@prefix} &7You were tempbanned by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_time}%&7 for <##f781ed>%{_reason}%&7."
		if arg-3 contains "-s":
			send "&7[SILENT] {@prefix} <##f781ed>%arg-1%&7 was banned by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_time}%&7 for <##f781ed>%{_reason}%&7." to all players where [input has permission "staff.notify"]
		else:
			broadcast "{@prefix} <##f781ed>%arg-1%&7 was banned by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_time}%&7 for <##f781ed>%{_reason}%&7."
		send "`%arg-1%` was tempbanned by `%player ? ""Console""%` for **%{_time}%** for **%{_reason}%**" to channel with id "{@punishment-channel}" with {@bot}

command /ban <offline player> <text>:
	permission: punish.ban
	permission message: &cNo Permission.
	usage: &7Usage: /ban <player> <reason> [-s]
	trigger:
		stop if arg-1 is op
		set {ban::%arg-1's uuid%::type} to "perm"
		set {ban::%arg-1's uuid%::by} to player ? "Console"
		set {_reason} to arg-2
		replace all " -s" with "" in {_reason}
		set {ban::%arg-1's uuid%::reason} to {_reason}
		kick arg-1 due to "{@prefix} &7You were permanently banned by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_reason}%&7."
		if arg-2 contains "-s":
			send "&7[SILENT] {@prefix} <##f781ed>%arg-1%&7 was banned by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_reason}%&7." to all players where [input has permission "staff.notify"]
		else:
			broadcast "{@prefix} <##f781ed>%arg-1%&7 was banned by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_reason}%&7."
		send "`%arg-1%` was banned by `%player ? ""Console""%` for **%{_reason}%**" to channel with id "{@punishment-channel}" with "{@bot}"

command /unban <offline player> [<text>]:
	permission: punish.unban
	permission message: &cNo Permission.
	usage: &7Usage: /unban <player> [-s]
	trigger:
		delete {ban::%arg-1's uuid%::*}
		if arg-2 contains "-s":
			send "&7[SILENT] {@prefix} <##f781ed>%arg-1%&7 was unbanned by <##f781ed>%player ? ""Console""%&7." to all players where [input has permission "staff.notify"]
		else:
			broadcast "{@prefix} <##f781ed>%arg-1%&7 was unbanned by <##f781ed>%player ? ""Console""%&7."
		send "`%arg-1%` unbanned by `%player ? ""Console""%`" to channel with id "{@punishment-channel}" with "{@bot}"

command /kick <player> <text>:
	permission: punish.kick
	permission message: &cNo Permission.
	usage: &7Usage: /kick <player> <reason> [-s]
	trigger:
		set {_reason} to arg-2
		replace all "-s" with "" in {_reason}
		kick arg-1 due to "{@prefix} &7You were kicked by <##f781ed>%player ? ""Console""% for <##f781ed>%{_reason}%&7."
		if arg-2 contains "-s":
			send "&7[SILENT] {@prefix} <##f781ed>%arg-1%&7 was kicked by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_reason}%&7." to all players where [input has permission "staff.notify"]
		else:
			broadcast "{@prefix} <##f781ed>%arg-1%&7 was kicked by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_reason}%&7."
		send {_reason}
		send "`%arg-1%` was kicked by `%player ? ""Console""%` for **%{_reason}%**" to channel with id "{@punishment-channel}" with {@bot}

command /tempmute <offline player> <text> <text>:
	permission: punish.tempmute
	permission message: &cNo Permission.
	usage: &7Usage: /tempmute <player> <time ex:24h> <reason> [-s]
	trigger:
		stop if arg-1 is op
		set {_time} to formatTime(arg-2)
		set {mute::%arg-1's uuid%::type} to "temp"
		set {mute::%arg-1's uuid%::by} to player ? "Console"
		set {mute::%arg-1's uuid%::time} to now
		set {mute::%arg-1's uuid%::span} to {_time}
		set {_reason} to arg-3
		replace all " -s" with "" in {_reason}
		set {mute::%arg-1's uuid%::reason} to {_reason}
		send "{@prefix} &7You were tempmuted by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_time}%&7 for <##f781ed>%{_reason}%&7." to arg-1
		if arg-3 contains "-s":
			send "&7[SILENT] {@prefix} <##f781ed>%arg-1%&7 was tempmuted by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_time}%&7 for <##f781ed>%{_reason}%&7." to all players where [input has permission "staff.notify"]
		else:
			broadcast "{@prefix} <##f781ed>%arg-1%&7 was tempmuted by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_time}%&7 for <##f781ed>%{_reason}%&7."
		send "`%arg-1%` was tempmuted by `%player ? ""Console""%` for **%{_time}%** for **%{_reason}%**" to channel with id "{@punishment-channel}" with "{@bot}"

command /mute <offline player> <text>:
	permission: punish.mute
	permission message: &cNo Permission.
	usage: &7Usage: /mute <player> <reason> [-s]
	trigger:
		stop if arg-1 is op
		set {mute::%arg-1's uuid%::type} to "perm"
		set {mute::%arg-1's uuid%::by} to player ? "Console"
		set {_reason} to arg-2
		replace all " -s" with "" in {_reason}
		set {mute::%arg-1's uuid%::reason} to {_reason}
		send "{@prefix} &7You were permanently muted by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_reason}%&7." to arg-1
		if arg-2 contains "-s":
			send "&7[SILENT] {@prefix} <##f781ed>%arg-1%&7 was muted by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_reason}%&7." to all players where [input has permission "staff.notify"]
		else:
			broadcast "{@prefix} <##f781ed>%arg-1%&7 was muted by <##f781ed>%player ? ""Console""%&7 for <##f781ed>%{_reason}%&7."
		send "`%arg-1%` was muted by `%player ? ""Console""%` for **%{_reason}%**" to channel with id "{@punishment-channel}" with "{@bot}"

command /unmute <offline player> [<text>]:
	permission: punish.unmute
	permission message: &cNo Permission.
	usage: &7Usage: /unmute <player> [-s]
	trigger:
		delete {mute::%arg-1's uuid%::*}
		if arg-2 contains "-s":
			send "&7[SILENT] {@prefix} <##f781ed>%arg-1%&7 was unmuted by <##f781ed>%player ? ""Console""%&7." to all players where [input has permission "staff.notify"]
		else:
			broadcast "{@prefix} <##f781ed>%arg-1%&7 was unmuted by <##f781ed>%player ? ""Console""%&7."
		send "`%arg-1%` was unmuted by `%player ? ""Console""%` for **%{_reason}%**" to channel with id "{@punishment-channel}" with "{@bot}"

command /clearchat [<text>]:
	permission: punish.clearchat
	permission message: &cNo Permission.
	trigger:
		loop 200 times:
			broadcast ""
		if arg-1 contains "-a":
			broadcast "{@prefix} &7The chat was cleared."
		else:
			broadcast "{@prefix} &7The chat was cleared by <##f781ed>%player ? ""Console""%&7."
		send "Chat was cleared by `%player ? ""Console""%`." to channel with id "{@punishment-channel}" with "{@bot}"

command /mutechat [<text>]:
	permission: punish.mutechat
	permission message: &cNo Permission.
	aliases: /chatmute
	trigger:
		if {chatmute} is not set:
			set {chatmute} to true
			if arg-1 contains "-a":
				broadcast "{@prefix} &7The chat was muted."
			else:
				broadcast "{@prefix} &7The chat was muted by <##f781ed>%player ? ""Console""%&7."
			send "Chat was muted by `%player ? ""Console""%`." to channel with id "{@punishment-channel}" with "{@bot}"
		else:
			delete {chatmute}
			if arg-1 contains "-a":
				broadcast "{@prefix} &7The chat was unmuted."
			else:
				broadcast "{@prefix} &7The chat was unmuted by <##f781ed>%player ? ""Console""%&7."
			send "Chat was unmuted by `%player ? ""Console""%`." to channel with id "{@punishment-channel}" with "{@bot}"